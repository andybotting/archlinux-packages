# Maintainer: Andy Botting <andy@andybotting.com>

_module='os-testr'
pkgname=('python-os-testr' 'python2-os-testr')
pkgver='0.8.0'
pkgrel='1'
pkgdesc='A testr wrapper to provide functionality for OpenStack projects'
arch=('any')
url="https://pypi.python.org/pypi/${_module}/${pkgver}"
license=('Apache')
makedepends=('git' 'python-setuptools' 'python2-setuptools')
checkdepends=('python-coverage' 'python2-coverage'
              'python-oslotest' 'python2-oslotest'
              'python-testscenarios' 'python2-testscenarios'
              'python-ddt' 'python2-ddt'
              'python-six' 'python2-six')
source=("git+https://git.openstack.org/openstack/${_module}#tag=${pkgver}")
sha256sums=('SKIP')

prepare() {
  cp -a "${srcdir}/${_module}"{,-py2}
}

build() {
  cd "${srcdir}/${_module}"
  python setup.py build

  cd "${srcdir}/${_module}-py2"
  python2 setup.py build
}

package_python-os-testr() {
  depends=('python-pbr' 'python-babel' 'python-testrepository'
           'python-subunit' 'python-testtools')
  cd "${srcdir}/${_module}"
  python setup.py install --root="${pkgdir}/" --optimize=1
}

package_python2-os-testr() {
  depends=('python2-pbr' 'python2-babel' 'python2-testrepository'
           'python2-subunit' 'python2-testtools')
  cd "${srcdir}/${_module}"-py2
  python2 setup.py install --root="${pkgdir}/" --optimize=1
  mv "${pkgdir}"/usr/bin/generate-subunit{,2}
  mv "${pkgdir}"/usr/bin/ostestr{,2}
  mv "${pkgdir}"/usr/bin/subunit-trace{,2}
  mv "${pkgdir}"/usr/bin/subunit2html{,2}
}

# vim:set ts=2 sw=2 et:

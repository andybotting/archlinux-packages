# Maintainer: Andy Botting <andy@andybotting.com>

pkgname=python-nectarallocationclient
pkgver=0.5.0
pkgrel=2
pkgdesc='A client for the Nectar Allocation API'
arch=('any')
url='https://github.com/NeCTAR-RC/python-nectarallocationclient'
license=('Apache')
depends=('python-oslo-utils' 'python-requests' 'python-keystoneauth1'
         'python-osc-lib')
checkdepends=('python-testrepository' 'python-mock' 'python-requests-mock'
              'python-testtools' 'python-pip')
source=("https://github.com/NeCTAR-RC/$pkgname/archive/$pkgver.tar.gz")
sha512sums=('72bd564a44440f5c04547de85a290f8dea5ef83365284671881db851c4ccc56510eb4eae9e2ea7d4201f11d7748fbfac43051f2e6f7c38519a9a4ffb8c752478')

export PBR_VERSION=$pkgver

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

check() {
  cd $pkgname-$pkgver
  python setup.py testr
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:

# $Id$
# Maintainer: Andy Botting <andy@andybotting.com>

#pkgbase=python-osc-lib
pkgname=(python-osc-lib python2-osc-lib)
pkgver=0.4.0
pkgrel=1
pkgdesc="OpenStack Command-line Client"
arch=('any')
url="http://docs.openstack.org/developer/osc-lib"
license=('Apache')
source=("git+https://git.openstack.org/openstack/osc-lib#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a osc-lib{,-py2}
}

build() {
  cd "$srcdir"/osc-lib
  python setup.py build

  cd "$srcdir"/osc-lib-py2
  python2 setup.py build
}

package_python-osc-lib() {
  depends=('python-six' 'python-pbr' 'python-babel' 'python-cliff' 'python-keystoneauth1'
           'python-os-client-config' 'python-oslo-i18n' 'python-oslo-utils' 
           'python-simplejson' 'python-stevedore')

  cd "$srcdir"/osc-lib
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-osc-lib() {
  depends=('python2-six' 'python2-pbr' 'python2-babel' 'python2-cliff' 'python2-keystoneauth1'
           'python2-os-client-config' 'python2-oslo-i18n' 'python2-oslo-utils' 
           'python2-simplejson' 'python2-stevedore')

  cd "$srcdir"/osc-lib-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
